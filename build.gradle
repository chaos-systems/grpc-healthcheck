plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.google.protobuf" version "0.8.10"
}

group 'io.chaossystems.grpc'
version '1.1.1'

repositories {
    jcenter()
    mavenCentral()
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.10.+'
    }

    generatedFilesBaseDir = "$projectDir/build/src/generated"
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.10.+'
    compile 'io.grpc:grpc-all:1.25.+'
}

task protoJar(type: Jar){
    classifier = "protos"
    from sourceSets.main.proto
}

task sourceJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
}

build.dependsOn protoJar
build.dependsOn sourceJar

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/chaos-systems/grpc-healthcheck")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR") ?: System.getenv("USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        PubBintray(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact protoJar
        }
        gpr(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact protoJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publish = true
    override = true

    publications = ['PubBintray']

    pkg {
        repo = 'mvn'
        name = project.name
        userOrg = 'chaos-systems'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/chaos-systems/grpc-healthcheck'
    }
}

